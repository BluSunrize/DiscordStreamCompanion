/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.1/userguide/tutorial_java_projects.html
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'me.trnl:github-release-gradle-plugin:0.1'
    }
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
// Apply the application plugin to add support for building an application
apply plugin: 'application'
//Github Plugin by trnl
apply plugin: 'github'

def mainVersion = '1.0'
def buildNumber = '9'

version = "${mainVersion}.${buildNumber}"
group= 'blusunrize'
archivesBaseName = 'DiscordStreamCompanion'
mainClassName = 'blusunrize.discordstreamcompanion.Main'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

task increment {
    group 'Build Setup'
    description 'Increases the Buildnumber'
    doLast {
        def bn = buildFile.getText().find(buildNumber) //get this build file's text and extract the buildNumber
        int inc = bn.toInteger() + 1
        String s = buildFile.getText().replaceFirst("def buildNumber = '" + bn + "'", "def buildNumber = '" + inc + "'")
        buildFile.setText(s) //replace the build file's text
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    compile 'com.google.guava:guava:22.0'
    compile 'net.dv8tion:JDA:3.2.0_250'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': mainClassName
    }
    doLast {
        tasks.increment.execute() //After all is done, increment the buildnumber for next time
    }
}

github {
    /* Wiki repo will be calculated from this. */
    repo = 'git@github.com:BluSunrize/DiscordStreamCompanion.git'

    release {
        /* Use {} brackets for lazy evaluation */
        tag = { "r$project.release.version" }
        name = { "$project.release.version" }
    }
}